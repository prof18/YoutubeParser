apply plugin: "com.github.ben-manes.versions"

buildscript {
    ext.versions = [
            'libVersionCode'     : 30003,
            'libVersionName'     : '3.0.3',
            'compileSdk'         : 30,
            'minSdk'             : 14,
            'targetSdk'          : 30,
            'okhttp'             : '3.12.0', // LTS version, update until December 31, 2021.
            'gson'               : '2.8.6',
            'coroutines'         : '1.4.2',
            'kotlin'             : '1.4.30',
            'buildTools'         : '30.0.1',
            'appCompat'          : '1.2.0',
            'constraintLayout'   : '2.0.4',
            'material'           : '1.3.0',
            'swipe'              : '1.1.0',
            'androidx'           : '1.0.0',
            'lifecycle'          : '2.3.0',
            'picasso'            : '2.71828',
            'activityAndroidx'   : '1.2.0',
            'recyclerView'       : '1.1.0',
            'lifecycleExtensions' : '2.2.0',

            'junit'              : '4.13.2',
            'extJunit'           : '1.1.0',
            'espresso'           : '3.1.1',
            'mockito'            : '2.23.0',

    ]

    ext.kotlin_version = '1.3.11'
    repositories {
        google()
        mavenCentral()
        maven {
            url  "https://dl.bintray.com/kotlin/dokka"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${versions.kotlin}"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.36.0"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.4.20"
    }
}

def getKey() {
    Properties properties = new Properties()
    File propertiesFile = project.file('local.properties')
    if (propertiesFile.exists()) {
        properties.load(propertiesFile.newDataInputStream())
        return properties.getProperty("ytKey")
    } else {
        return ''
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url = "https://dl.bintray.com/kotlin/dokka"
        }
        // org.jetbrains.trove4j:trove4j:20160824.
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    // Example 1: reject all non stable versions
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}